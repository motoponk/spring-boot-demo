buildscript {
	ext {
		springBootVersion = '2.0.1.RELEASE'
	}
	repositories {
		mavenCentral()
		maven { url "https://repo.spring.io/snapshot" }
		maven { url "https://repo.spring.io/milestone" }
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
	}
}


plugins {
	id "com.gorylenko.gradle-git-properties" version "1.4.17"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'checkstyle'
apply plugin: "jacoco"

jar {
	baseName = 'movie-finder'
}

version = '1.0.0-SNAPSHOT'
sourceCompatibility = 1.8

repositories {
	mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

sourceSets {
	test {
		java.srcDirs = ['src/test/unit/java']
		resources.srcDirs = ['src/test/unit/resources']
	}
	integrationTest {
		java.srcDirs = ['src/test/integration/java']
		resources.srcDirs = ['src/test/integration/resources']
		compileClasspath += main.output + test.output
		runtimeClasspath += main.output + test.output
	}
}

configurations {
	integrationTestCompile.extendsFrom testCompile
	integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
	compile "org.springframework.boot:spring-boot-starter-actuator"
	compile "org.springframework.boot:spring-boot-starter-web"
	compile "org.springframework.boot:spring-boot-starter-thymeleaf"
	compile "org.springframework.boot:spring-boot-starter-data-jpa"
	compile "mysql:mysql-connector-java"
	compile "com.h2database:h2"
	compile "org.projectlombok:lombok"
	compile "org.springframework.boot:spring-boot-devtools"
	compile "org.springframework.boot:spring-boot-configuration-processor"
	testCompile "org.springframework.boot:spring-boot-starter-test"
	compile "org.webjars:bootstrap:3.3.7"
	compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'org.postgresql', name: 'postgresql', version: '42.1.4'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.7'
    runtime("io.micrometer:micrometer-registry-prometheus")

}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			limit {
				minimum = 0.3
			}
		}
	}
}

// to run coverage verification during the build (and fail when appropriate)
check.dependsOn jacocoTestCoverageVerification

jacocoTestReport {
	reports {
		html {
			enabled true
		}
	}
	afterEvaluate {
		classDirectories = files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [ '**/config/*', '**/entity/*', '**/*Application*' ])
		})
	}
}

test {
	/*
	testLogging {
		events "passed", "skipped", "failed", "standardOut", "standardError"
	}
	*/

	jacoco {
		append = false
		destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
		classDumpDir = file("$buildDir/jacoco/classpathdumps")
	}
}

import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

tasks.withType(Test) {
	testLogging {
		// set options for log level LIFECYCLE
		events TestLogEvent.FAILED,
				TestLogEvent.PASSED,
				TestLogEvent.SKIPPED,
				TestLogEvent.STANDARD_OUT
		exceptionFormat TestExceptionFormat.FULL
		showExceptions true
		showCauses true
		showStackTraces true

		// set options for log level DEBUG and INFO
		debug {
			events TestLogEvent.STARTED,
					TestLogEvent.FAILED,
					TestLogEvent.PASSED,
					TestLogEvent.SKIPPED,
					TestLogEvent.STANDARD_ERROR,
					TestLogEvent.STANDARD_OUT
			exceptionFormat TestExceptionFormat.FULL
		}
		info.events = debug.events
		info.exceptionFormat = debug.exceptionFormat

		afterSuite { desc, result ->
			if (!desc.parent) { // will match the outermost suite
				def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
				def startItem = '|  ', endItem = '  |'
				def repeatLength = startItem.length() + output.length() + endItem.length()
				println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
			}
		}
	}
}

task integrationTest(type: Test, description: 'Runs the integration tests.', group: 'Verification') {
	testClassesDirs = sourceSets.integrationTest.output.classesDirs
	classpath = sourceSets.integrationTest.runtimeClasspath
}

integrationTest {
	systemProperties = System.properties
}

springBoot  {
	buildInfo {
		properties {
			additional = [
					'Author': 'SivaLabs'
			]
		}
	}
}

bootJar  {
	classifier = 'exec'
}